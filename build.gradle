buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/maven/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version project.modVersion + "-" + 'git rev-parse --short HEAD'.execute().text.trim()
group project.modGroup

archivesBaseName = "lambda"

tasks.register('writeVersion') {
    group 'build'
    description 'Write version to file'

    doLast {
        if (project.version.length() > 8) {
            var f = file(project.buildDir.absolutePath + '/resources/main/lambda_version.txt')
            f.parentFile.mkdirs()
            println "Writing version: " + project.version
            f.write(project.version)
        } else {
            println "Unable to determine full version"
        }
    }
    outputs.upToDateWhen { false }
}

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
    // Disables Gradle build caching for this task
    // If build caching is enabled this can cause the refmap to not be built and included
    outputs.upToDateWhen { false }
}

compileKotlin.kotlinOptions {
    freeCompilerArgs += '-Xlambdas=indy'
    freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    freeCompilerArgs += '-opt-in=kotlin.contracts.ExperimentalContracts'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url = 'https://impactdevelopment.github.io/maven/' }
    maven { url = 'https://jitpack.io' }
}

minecraft {
    mappings channel: "$mappingsChannel", version: "$mappingsVersion"

    runs {
        client {
            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'com.lambda.client.LambdaCoreMod'
            property 'mixin.env.disableRefMap', 'true' // Disable refmap so we don't get trolled by Baritone

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    jarLibs
    onlyJarLibs
    // Force choosing the correct nightly build because Mac OS chooses an invalid one
    configureEach {
        resolutionStrategy {
            force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
        }
    }
}

dependencies {
    // Forge
    minecraft "net.minecraftforge:forge:$minecraftVersion-$forgeVersion"

    jarLibs('org.spongepowered:mixin:0.8.5') {
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
    }

    // Hacky way to get mixin work
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor') {
        exclude module: 'gson'
    }

    // Not the latest Reflections because it breaks Future compatibility :/
    //noinspection GradlePackageUpdate
    jarLibs 'org.reflections:reflections:0.9.12'

    jarLibs("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion") {
        exclude module: 'kotlin-stdlib-common'
        exclude module: 'annotations'
    }

    jarLibs("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion") {
        exclude module: 'kotlin-stdlib'
    }

    jarLibs("org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion") {
        exclude module: 'kotlin-stdlib-jdk8'
        exclude module: 'kotlin-stdlib-common'
    }

    jarLibs 'com.github.cbyrneee:DiscordIPC:e18542f600'

    // Add them back to compileOnly (provided)
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"
    compileOnly 'org.jetbrains:annotations:23.0.0'

    // This Baritone will NOT be included in the jar
    implementation files("libs/baritone-deobf-1.2.17.jar")

    // This Baritone WILL be included in the jar
    onlyJarLibs files("libs/baritone-api-forge-1.2.17.jar")

    // Add everything in jarLibs to implementation (compile)
    implementation configurations.jarLibs
}

sourceSets {
    schematica_api {
        compileClasspath += main.compileClasspath
    }

    main {
        compileClasspath += schematica_api.output
    }
}

mixin {
    defaultObfuscationEnv 'searge'
    sourceSets {
        main {
            ext.refMap = 'mixins.lambda.refmap.json'
        }
    }
}

processResources {
    exclude '**/rawimagefiles'

    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include 'mcmod.info'
        expand version: version, 'mcversion': minecraftVersion
    }
    finalizedBy 'writeVersion'
}

tasks.register('buildApiSource', Jar) { // Generate sources
    group 'build'
    description 'Assemble API library source archive'

    archiveClassifier.set 'api-source'
    from sourceSets.main.allSource
}

tasks.register('buildApi', Jar) {
    group 'build'
    description 'Assemble API library archive'

    archiveClassifier.set 'api'
    from sourceSets.main.output
}

tasks.register('buildAll') {
    group 'build'
    description 'Assemble all jars'

    dependsOn 'buildApi'
    dependsOn 'buildApiSource'
    dependsOn 'build'
}

tasks.register('clearLibs') {
    group 'build'
    description 'Clears the libs folder'

    doLast {
        file(project.buildDir.absolutePath + "/libs").deleteDir()
    }
}

shadowJar {
    // Don't put baritone mixin here please c:
    manifest.attributes(
            'Manifest-Version': 1.0,
            'MixinConfigs': 'mixins.lambda.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'com.lambda.client.LambdaCoreMod',
            'ForceLoadAsMod': 'true'
    )

    archiveClassifier.set('')
    exclude '**/module-info.class',
            'DebugProbesKt.bin',
            'META-INF/proguard/**',
            'META-INF/versions/**',
            'META-INF/**.RSA',
            'META-INF/com.android.tools/**',
            'META-INF/*.kotlin_module',
            'kotlin/**/*.kotlin_metadata',
            'kotlin/**/*.kotlin_builtins',
            'META-INF/*.version'
    configurations = [project.configurations.jarLibs, project.configurations.onlyJarLibs]
    relocate 'kotlin', 'com.lambda.shadow.kotlin'
    relocate 'kotlinx', 'com.lambda.shadow.kotlinx'
    finalizedBy 'configureReobfTaskForReobfShadowJar', 'reobfShadowJar'
    mustRunAfter 'jar'
    dependsOn 'clearLibs'
}

reobf {
    shadowJar {}
    jar {
        enabled = false
    }
}

task setGameDir() {
    // Check if custom gamedir has been passed, if not use default ones per platform
    if (!project.hasProperty("gameDir")) {
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            project.ext.gameDir = System.getenv("APPDATA") + "/.minecraft"
        } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            project.ext.gameDir = System.properties["user.home"] + "/Library/Application Support/minecraft"
        } else {
            project.ext.gameDir = System.properties["user.home"] + "/.minecraft"
        }
    }
}

/**
 * Courtesy of ForgeHax
 */
class CopyJarTask extends DefaultTask {
    CopyJarTask() {
        group = "build"
    }
    @TaskAction
    protected void exec() {
        project.copy {
            final gameDir = project.ext.gameDir;
            final modDir = "$gameDir/mods"

            // find previous jar (if it exists)
            final previousJars = project.fileTree(dir: modDir).matching {
                include project.archivesBaseName + '*'
            }.files

            // change file name
            if (previousJars) {
                // create .backups folder
                final backupDir = modDir + '/.backups/'
                project.file(backupDir).mkdirs()

                for (File previousJar : previousJars) {
                    final n = modDir + '/.backups/' + previousJar.getName() + '.bk'
                    def f = project.file(n)
                    def i = 1
                    while (f.exists()) {
                        f = project.file(n + '_' + i)
                        i++
                    }
                    previousJar.renameTo(f)
                }
            }

            from project.jar
            into "$gameDir/mods"
        }
    }
}

task copyJar(type: CopyJarTask, dependsOn: [setGameDir, build])

publishing {
    publications {
        api(MavenPublication) {
            artifact source: buildApi, classifier: null
            artifact source: buildApiSource, classifier: 'sources'
        }
    }
}
